! Toto je komentar 

FINISH  			! Skoncit procesory 
/CLEAR				! Nova analyza

!/PNUM, LINE, 1
!/PNUM,KP,1

CURVE = 2e-4		! Datova premena
KEYW,PR_SET,1   	! 1.) preferences --> typ analizy 1 = mechanicka analyza
KEYW,PR_STRUC,1 	! structural

/PREP7				! Enter preprocessor  --> /element type/add

ET,1,PLANE182   	! 2.) a.) element type,  2D, 4-node mechanika
KEYOPT,1,3,3		! plane with thickness - keyoptioon c. 3 na hodnotu 3 (with thicknes)


R,1,1e-3,   		! 2.) b.) real constatnt cislo 1 pre thk = 1 mm, konstatnta pre hrubku telesa


MP, EX, 1, 140e9	! 3.) Modul pruznosti materialu = 140 GPa (pre material cislo 1)
MP, PRXY, 1, 0.23	! poasonovo cislo pre material 1, y = 0.23


! 4.) GEOMETRIA	! vytvorenie bodov v kartezianskom suradnom systeme

K,,4e-3, 1e-3
K,,3.6e-3, 1e-3  
K,,3.6e-3, 1.8e-3
K,,3.8e-3, 1.8e-3
K,,3.8e-3, 2.2e-3
K,,3.2e-3, 2.2e-3
K,,3.2e-3, 6e-3
K,,4e-3,6e-3
K,,4e-3,7e-3
K,,3e-3,7e-3
K,,3e-3,2.2e-3
K,,2.2e-3,2.2e-3
K,,2.2e-3,7e-3
K,,3e-3,8e-3
K,,3e-3,9e-3
K,,2.8e-3,9e-3
K,,2.8e-3,8e-3
K,,1.8e-3,7e-3
K,,1.8e-3,1.8e-3
K,,3.4e-3,1.8e-3
K,,3.4e-3,1e-3
K,,1e-3,1e-3
K,,1e-3,7e-3
K,,0.7e-3,7e-3
K,,0e-3,6e-3
K,,0e-3,0e-3
K,,4e-3,0e-3


!/PNUM,KP,1			! Cislovanie keypointov

*DO,i,1,26 			! Cyklus pocitadlo od 0 po 26 s krokom 1
L,i, i+1			! Spoji/vykresli ciari medzi bodmi v poradi
*ENDDO				! Koniec cyklu

L, 27,1				! Posledne spojenie - zaciat. a konc. bodu


! ZAOBLENIE HRAN - FILLETING

LFILLT, 5, 6, CURVE	
LFILLT, 10, 11, CURVE 
LFILLT, 1, 2, CURVE 
LFILLT, 2, 3, CURVE 
LFILLT, 19, 20, CURVE 
LFILLT, 21, 20, CURVE 

!/eof 				! End of file --> for debug

AL, ALL				! Area by lines - pomocou vsetkych vybratych ciar vytvori plochu


! 5.) MESHING --> tvorba sieti elemnetarnych prvkov

AESIZE, 1, 5e-5		!nastavenie velkosti elementu pre plochu 1

AMESH, ALL			!vymeshuj vsetky plochy
!AMESH, 1			!vymeshuju plochu 1


! 6.) OKRAJOVE PODMIENKY

DL,26,1,ALL,0		! votknutie (displacement 0) ALLDOF, pre ciaru c. 26 z plochy 1
DL,9,1,UY,20e-6		! mechanicke namahanie -- vznikne posunutie ciary 9 v y smere o 20 um
DL, 27,1, SYMM		! pre zachovanie symetrie telesa treba votknut plochy symetrie
DL, 8, 1, UX, 0		! dalsia strana symetrie


FINISH				! ukoncenioe proprocesora
/SOL				! vojdem to solution procesora

SOLVE				! prikaz solve
